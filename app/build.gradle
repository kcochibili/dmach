import net.simno.dmach.Libs
import net.simno.dmach.Versions

apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.github.triplet.play'

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

play {
    track = 'internal'
    resolutionStrategy = 'ignore'
    defaultToAppBundles = true
    serviceAccountCredentials = rootProject.file('signing/play-account.json')
}

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    ndkVersion Versions.ndk
    defaultConfig {
        applicationId 'net.simno.dmach'
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode 20006
        versionName '2.0'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
    viewBinding {
        enabled = true
    }
    signingConfigs {
        release {
            storeFile rootProject.file('signing/app-upload.jks')
            storePassword System.getenv('STORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/proguard/*'
        exclude 'META-INF/*.version'
        exclude 'META-INF/*.properties'
        exclude '/*.properties'
    }
    compileOptions {
        sourceCompatibility Versions.java
        targetCompatibility Versions.java
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation Libs.Kotlin.stdlib
    implementation Libs.Kotlin.serializationRuntime

    implementation Libs.AndroidX.activity
    implementation Libs.AndroidX.appcompat
    implementation Libs.AndroidX.constraintlayout
    implementation Libs.AndroidX.core
    implementation Libs.AndroidX.dynamicanimation
    implementation Libs.AndroidX.Lifecycle.extensions
    implementation Libs.AndroidX.Lifecycle.viewmodel
    implementation Libs.AndroidX.Paging.runtime
    implementation Libs.AndroidX.Paging.rxjava2
    implementation Libs.AndroidX.recyclerview
    implementation Libs.AndroidX.Room.runtime
    implementation Libs.AndroidX.Room.rxjava2
    kapt Libs.AndroidX.Room.compiler

    implementation Libs.Google.ossLicensesLibrary

    implementation Libs.rxbinding
    implementation Libs.rxrelay
    implementation Libs.rxandroid
    implementation Libs.rxjava

    implementation Libs.dmachExternals
    implementation Libs.kortholt

    testImplementation Libs.AndroidX.Test.core
    testImplementation Libs.AndroidX.Test.junit
    testImplementation Libs.AndroidX.Test.runner
    testImplementation Libs.AndroidX.Paging.common
    testImplementation Libs.junit
    testImplementation Libs.mockito
    testImplementation Libs.robolectric
    testImplementation Libs.Google.truth
}
