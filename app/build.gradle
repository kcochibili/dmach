apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'checkstyle'

checkstyle {
    toolVersion = '8.26'
}

task checkstyle(type: Checkstyle) {
    group 'verification'
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

afterEvaluate {
    if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
    }
}

play {
    track = 'internal'
    resolutionStrategy = 'ignore'
    defaultToAppBundles = false
    serviceAccountCredentials = rootProject.file('signing/play-account.json')
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "net.simno.dmach"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 16023
        versionName '1.6'
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile rootProject.file('signing/app-upload.jks')
            storePassword System.getenv('STORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.sqlite:sqlite:2.0.1'
    implementation 'androidx.sqlite:sqlite-framework:2.0.1'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.dagger:dagger:2.25.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.25.2'

    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.squareup.sqlbrite3:sqlbrite:3.2.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.13'

    implementation 'org.parceler:parceler-api:1.1.12'
    annotationProcessor 'org.parceler:parceler:1.1.12'

    implementation 'org.puredata.android:pd-core:1.1.0'
    implementation 'net.simno.dmach:dmach-externals:1.2.3'
}
