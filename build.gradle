import net.simno.dmach.Libs
import net.simno.dmach.Versions

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.playPublisherPlugin
        classpath Libs.Google.ossLicensesPlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Kotlin.serializationPlugin
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.26.1'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://kotlin.bintray.com/kotlinx' }
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = Versions.java
            allWarningsAsErrors = true
            freeCompilerArgs += ['-Xuse-experimental=kotlin.Experimental']
        }
    }
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(Versions.ktlint)
        }
    }
}
