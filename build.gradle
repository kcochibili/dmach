buildscript {
    ext.versions = [
            'kotlin'        : '1.3.60',
            'kotlinx_serial': '0.13.0',
            'compile_sdk'   : 29,
            'target_sdk'    : 29,
            'min_sdk'       : 21,
            'build_tools'   : '29.0.2',
            'androidx'      : [
                    'activity'        : '1.1.0-rc02',
                    'appcompat'       : '1.1.0',
                    'constraintlayout': '2.0.0-beta3',
                    'core'            : '1.2.0-beta02',
                    'dynamicanimation': '1.1.0-alpha02',
                    'lifecycle'       : '2.2.0-rc02',
                    'paging'          : '2.1.0',
                    'recyclerview'    : '1.1.0-rc01',
                    'room'            : '2.2.1',
                    'test_core'       : '1.2.1-alpha02',
                    'test_junit'      : '1.1.2-alpha02',
                    'test_runner'     : '1.3.0-alpha02'
            ],
            'oss_licenses'  : '17.0.0',
            'ktlint'        : '0.33.0',
            'rxandroid'     : '2.1.1',
            'rxjava'        : '2.2.14',
            'rxbinding'     : '3.0.0',
            'rxrelay'       : '2.1.1',
            'kortholt'      : '1.1.4',
            'dmach_ext'     : '1.2.4',
            'junit'         : '4.13-rc-1',
            'mockito'       : '3.1.0',
            'robolectric'   : '4.3.1',
            'truth'         : '1.0'
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.0-beta04'
        classpath 'com.github.triplet.gradle:play-publisher:2.6.0'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$versions.kotlin"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.26.0'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://kotlin.bintray.com/kotlinx' }
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '1.8'
            allWarningsAsErrors = true
            freeCompilerArgs += ['-Xuse-experimental=kotlin.Experimental']
        }
    }
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint)
        }
    }
}
